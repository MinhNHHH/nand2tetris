// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   	// Put you code here:
		Not16(in=x, out=notX);
		Not16(in=y, out=notY);

		// Decide x is zero or x.
		Mux16(a=x, b=false, sel=zx, out=zx16);
		// Negative x
		Not16(in=zx16, out=notzx16);
		Mux16(a=zx16, b=notzx16, sel=nx, out=x16);

		Mux16(a=y, b=false, sel=zy, out=zy16);
		Not16(in=zy16, out=notzy16);
		Mux16(a=zy16, b=notzy16, sel=ny, out=y16);

		Add16(a=x16, b=y16, out=add16);
		And16(a=x16, b=y16, out=and16);

		Mux16(a=and16, b=add16, sel=f, out=f16);
		Not16(in=f16, out=notf16);
		
		// out[15] is first position from right to left that bit is negative.
		Mux16(a=f16, b=notf16, sel=no, out=out, out[15]=ng ,out[0..7]=out1, out[8..15]=out2);

		Or8Way(in=out1, out=leftF);
		Or8Way(in=out2, out=rightF);

		// Sum results of function f.
		Or(a=leftF, b=rightF, out=orZr);
		Not(in=orZr, out=zr);
}